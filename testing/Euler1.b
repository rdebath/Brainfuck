// The first chunk is just something to print the heading text
++++++++++[>+++++++++++>++++++++++>+++>++++++++++++>+++++>+>++++++++>+++
+>+++++++>++++++<<<<<<<<<<-]>>>>>>>.<<<<<<++++.---.-----.>+.--.>>----.<+
+.>>>>>>-.<<<<<+.<<<++.>++.<++++++.>>.>>----.<<<<----.>.>>-.<.>>>>.<<<<<
<++++.---.>---.<---.>+++.<+.>>.>>+++.>..>---.<<<+.<<<-.>>>-.<<<---.+++++
++.----.>.>>-.<.<<+++.>+.>.>>++.<<.<-----.<-.>+++.>.>>++.>.>>>++++.<<<<<
<<++.>.>++++.<<-.>.<<--.---.>>>----.+.<.<----.<+++..>>.>.<<+++++++.---.>
.<<++.>----.>>.+.<<<++++.>.<------.>>.<<++.>>>.<<<-.>+.+++.<+++++.>>>--.
<.<---.+++.<------.+++.>>>++++.<.>>----.-.<<.>---.<<+++.-------.>>.<.<.<
+++.>++++.>.<<-----.>>>+.<<<-.>>>-.<<<---.+++++++.----.>.>>-.<.<<+++.>+.
>.>>+++.<<.<<.+++.>>.>>++.>>>++++.<<.<<++++.<<-.>.<++.--.>>---.<.>>--.>>
>.<<<<<.>>++.>>>.<<<<<.>>+.<<.<----.<----.>+++.>.>>>>>>>---.<<<<<-------
-.<<.>>>>+++++++.<<<<<++++.---.>.>-.++.<<<-.>>.<<++.>+.>.>-.<<++.---.>>-
.<<.>.<<--.>>>++.<<<-.>>>-.<<<---.+++++++.----.>.>>-.<.<<---.>>>.<.>>+++
+.+.-----.>..>>>---.<<<<<<<<.+++++.>-.>.>+.<<++++.---.>.>-.++.<<<-.>>.<<
++.>+.>.<-----.<---..>>.>-.<<+++++++.---.>.<<+.>>>+.<<<-.>>>-.<<<---.+++
++++.----.>.>>-.<.<<+++.>+.>.>>+++++.<<.<<.+++.>>.>>++.<<.<----.+++.<---
---.+++.>>>++++.<.>>----.-...--.>.<...>..>>>>[-]<[-]<[-]<[-]<[-]<[-]<[-]
<[-]<[-]<[-]<

Project Euler Problem 1 by James Stanley

Generate 1000
++++++++++[>++++++++++[>++++++++++<-]<-]>>
Take it down to 995
-----

Memory now: === 0 0 (995) result=0 ===

repeatedly add the number at 995 to our result and then take 5 off 995

[[->+>+<<] >>[-<<+>>]<< -----]

Move back to address 0
<<

Memory now: === (0) 0 0 result=99500 ===

Generate 1000
++++++++++[>++++++++++[>++++++++++<-]<-]>>
Take it down to 999
-

Memory now: === 0 0 (999) result=99500 ===

for 3's we do the same as the loop above for 5's but remember to ignore the
ones that were counted for 5 and basically unroll the loop and only count it on
the ones that aren't divisible by 15
also we quit looping when the outer loop should end

[
  <[-]<[-]>> [-<+<+>>] <<[->>+<<]>> < [[->>+>+<<<] >>>[-<<<+>>>]<< --- < [-]] >
  <[-]<[-]>> [-<+<+>>] <<[->>+<<]>> < [[->>+>+<<<] >>>[-<<<+>>>]<< --- < [-]] >
  <[-]<[-]>> [-<+<+>>] <<[->>+<<]>> < [[->>+>+<<<] >>>[-<<<+>>>]<< --- < [-]] >
  <[-]<[-]>> [-<+<+>>] <<[->>+<<]>> < [> --- < [-]] >
  <[-]<[-]>> [-<+<+>>] <<[->>+<<]>> < [[->>+>+<<<] >>>[-<<<+>>>]<< --- < [-]] >
]

Memory now: === 0 0 (0) result=233168 0 ===

[
Print overall result
>.
]

////////////////////////////////////////////////////////////////////////////////

> Move to result cell and print as a number
>[-]>[-]>[-]>[-]>[-]>[-]+>[-]+>[-]+>[-]+<<<<<<<<<[->>+>+<<<]>>>[-<<<+>>>
]<[>[-]>[-]<<[>+>+<<-]>>[<<+>>-]+++++++++<[>>>[-]+<<[>+>[-]<<-]>[<+>-]>[
<<++++++++++>>-]<<-<-]+++++++++>[<->-]<[>+<-]<[>+<-]<[>+<-]>>>[<<<+>>>-]
>>>[-]<<<+++++++++<[>>>[-]+<<[>+>[-]<<-]>[<+>-]>[<<++++++++++>>>+<-]<<-<
-]>>>>[<<<<+>>>>-]<<<[-]<<+>]<[>[-]<[>+<-]+++++++[<+++++++>-]<-.[-]>>[<<
+>>-]<<-]<

<++++++++++.
